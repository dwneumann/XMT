#!/usr/bin/env perl 
#************************************************************************
#   Package	: xmt_util
#  usage: backup -[0|1] [--dest=<dstpath>] srcpath 
#  Create a tar archive of the specified srcpath to the specified dstpath.
#  If -0 is specified a level 0 archive is created.
#  If -1 is specified a level 1 listed-incremental archive is created.
#  In all cases we exclude files matching patterns specified in ./backup.exclude
#  The command is logged to /var/logs/backup.log
#
#  Backups are host, level and date-stamped
#  (i.e.  /Backups/M6800.home.2019-11-01.0.tgz)
#  
#  Here is an example crontab:
#	@weekly		backup -0 /home
#	@daily		backup -1 /home
#
#  Deleting of old backups is easily done with a find command:
#	@monthly  find /Backups -maxdepth 0 -ctime 365 -delete   # delete backups over 1yr old
#
#    Copyright 2019 Visionary Research Inc.   All rights reserved.
#    			legal@visionary-research.com
#*  Licensed under the Apache License, Version 2.0 (the "License");
#*  you may not use this file except in compliance with the License.
#*  You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
#*  
#*  Unless required by applicable law or agreed to in writing, software
#*  distributed under the License is distributed on an "AS IS" BASIS,
#*  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#*  See the License for the specific language governing permissions and
#*  limitations under the License. 
#************************************************************************

use strict;
use Getopt::Long;
use File::Basename;
use POSIX qw(strftime tzset);
use Env;

(my $pgmname = $0) =~ s{.*/}{};
my $usage	= "usage: $pgmname [-u] [-0|1] [--dest=<path>] <path>\n";
my $logfile	= "/var/log/backup.log";
my $excludes	= "./$0.exclude";			# search in same directory as pgm
my $hostname	=`/bin/hostname`; chomp($hostname);
my $dstroot	= "/cygdrive/c/Backups/$hostname";	# unless overridden by --dest
$ENV{'TZ'}	= "America/Vancouver"; tzset();		# necessary for Windows OS

##   there's nothing configurable below this line.  Leave it alone.
my ($opt_u, $opt_full, $opt_incr);
GetOptions( "u" => \$opt_u, "dest=s" => \$dstroot, 
	    "0" => \$opt_full, "1" => \$opt_incr ) or die $usage;
die "$usage" if defined $opt_u;
my $level = (defined $opt_incr ? 1: 0);
my $srcpath = $ARGV[$#ARGV]	or die $usage;
$srcpath =~ s:/$::;		# strip trailing slash
open(my $LOG, ">>", $logfile) or die "$logfile: $!";
$LOG->autoflush(1); 

my ($basename, $path, $sfx)	= fileparse($srcpath);
my $today = strftime("%F", localtime);
my $timenow = strftime("%X", localtime);
print $LOG "\n\n______________________________________________________________\n";
print $LOG "level $level backup of $srcpath started $today at $timenow \n";
my $dstpath = "$dstroot/$hostname.$basename.$today.$level";
my @fullbackups = glob( "$dstroot/$hostname.$basename.*.0.tgz");
my $last0 = $fullbackups[0];
backup($srcpath, $dstpath, $excludes, $last0);
my $timenow = strftime("%X", localtime);
print $LOG "... ended at $timenow \n";

exit 0;


sub backup
{
    my ($srcpath, $dstpath, $excludes, $last0) = @_;
    
    my $cmd = "tar --create --file=$dstpath.tgz --directory=$srcpath ";
    $cmd .= " --exclude-from=$excludes" if (-e $excludes);
    $cmd .= " --newer=$last0" if ($level > 0 && -e $last0);
    $cmd .= " --use-compress-program=pigz";
    $cmd .= " --ignore-failed-read --exclude-backups --seek --totals";
    $cmd .= " --record-size=65536 --checkpoint=160000 ";  # 64KB writes, 10GB checkpoints
    $cmd .= " --checkpoint-action=totals";
    $cmd .= " .";
    print $LOG "$cmd \n";
    system("$cmd >>$logfile 2>&1");
}
